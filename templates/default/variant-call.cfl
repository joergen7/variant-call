% VARIANT-CALL
%
% a variant calling workflow
%
%
% Copyright:: 2015-2018 JÃ¶rgen Brandt
%
% Licensed under the Apache License, Version 2.0 (the "License");
% you may not use this file except in compliance with the License.
% You may obtain a copy of the License at
%
%    http://www.apache.org/licenses/LICENSE-2.0
%
% Unless required by applicable law or agreed to in writing, software
% distributed under the License is distributed on an "AS IS" BASIS,
% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
% See the License for the specific language governing permissions and
% limitations under the License.
%
%
% Sample data can be obtained from:
% ftp://ftp.1000genomes.ebi.ac.uk/vol1/ftp/phase3/data/HG02025/sequence_read/
%
% The HG38 reference genome can be downloaded from
% http://hgdownload.soe.ucsc.edu/goldenPath/hg38/chromosomes/
%
% An Annovar HG38 database is expected to reside in
% /opt/data/annodb_hg38
%
% In addition to a Cuneiform interpreter the following tools need to be
% installed to run this analysis:
% - FastQC 0.11.4
% - Bowtie2 2.2.6
% - SAMtools 1.2
% - VarScan 2.3.9
% - Annovar
%
%--------------------------------------------------------------
 
%% ============================================================
%% Task definitions
%% ============================================================

def split( file : File ) -> <lst : [File]> in Bash *{
  split -l 5120000 $file txt
  lst=txt*
}*

def untar( tar : File ) ->
      <fileLst : [File]>

in Bash*{
  tar xf $tar
  fileLst=`tar tf $tar`
}*


def gunzip( gz : File ) ->
      <file : File>

in Bash *{
  file=unzipped_${gz%.gz}
  gzip -c -d $gz > $file
}*


def fastqc( fastq : File ) ->
      <zip : File>

in Bash *{
  fastqc -f fastq --noextract -o ./ $fastq
  zip=`ls *.zip`
}*


def bowtie2Build( fa : File ) ->
      <idx : File>

in Bash *{
  bowtie2-build $fa bt2idx
  idx=idx.tar
  tar cf $idx --remove-files bt2idx.*
}*


def bowtie2Align( idx : File, fastq1 : File, fastq2 : File ) ->
      <bam : File>

in Bash *{
  tar xf $idx
  bam=alignment.bam
  bowtie2 -D 20 -R 3 -N 0 -L 20 -i S,1,0.50 --no-unal -x bt2idx \
  -1 $fastq1 -2 $fastq2 -S - | samtools view -b - > $bam
  rm bt2idx.*
}*


def samtoolsFaidx( fa : File ) ->
      <fai : File>

in Bash *{
  samtools faidx $fa
  fai=$fa.fai
}*


def samtoolsSort( bam : File ) ->
      <sorted : File>

in Bash *{
  sorted=sorted.bam
  samtools sort -m 2G $bam -o $sorted
}*


def samtoolsMpileup( bam : File, fa : File, fai : File ) ->
      <mpileup : File>

in Bash *{
  ln -sf $fai $fa.fai
  mpileup=mpileup.csv
  samtools mpileup -f $fa $bam > $mpileup
}*


def samtoolsMerge( bamLst : [File] ) ->
      <merged : File>

{

  def samtoolsMerge( bamLst : [File] ) ->
        <merged : File>

  in Bash *{
    merged=merged.bam
    if [ ${#bamLst[@]} -eq "1" ]
    then
      merged=$bam
    else
      samtools merge -f $merged ${bamLst[@]}
    fi
  }*


  if isnil bamLst
  then
    error "Merge list must not be empty." : <merged : File>
  else
    samtoolsMerge( bamLst = bamLst )
  end
}


def varscan-snp( mpileup : File ) ->
      <vcf : File>

in Bash *{
  vcf=snp.vcf
  varscan mpileup2snp $mpileup --output-vcf > $vcf
}*


def varscan-indel( mpileup : File ) ->
      <vcf : File>

in Bash *{
  vcf=indel.vcf
  varscan mpileup2indel $mpileup --output-vcf > $vcf
}*


def annovar( vcfLst : [File], db : File, buildVsn : Str ) ->
      <fun : File, exonicFun : File>

in Bash *{
  fun=table.variant_function
  exonicFun=table.exonic_variant_function
  tar xvf $db
  cat ${vcfLst[@]} | \
  convert2annovar.pl -format vcf4 - | \
  annotate_variation.pl -buildver $buildVsn -geneanno -outfile table - db
}*


%% ============================================================
%% Input data
%% ============================================================

let hg38Tar : File =
  'hg38/hg38.tar';

let fastq1LstGz : [File] =
  [
  'kgenomes/SRR359188_1.filt.fastq.gz',
  'kgenomes/SRR359195_1.filt.fastq.gz' : File];

let fastq2LstGz : [File] =
  [
  'kgenomes/SRR359188_2.filt.fastq.gz',
  'kgenomes/SRR359195_2.filt.fastq.gz' : File];

let buildVsn : Str =
  "hg38";

let db : File =
  'annovar/hg38db.tar';




%% ============================================================
%% Workflow definition
%% ============================================================

let <fileLst = faLst : [File]> =
  untar( tar = hg38Tar );

let fastq1Lst : [File] =
  for gz : File <- fastq1LstGz do
    ( gunzip( gz = gz )|file ) : File
  end;

let fastq2Lst : [File] =
  for gz : File <- fastq2LstGz do
    ( gunzip( gz = gz )|file ) : File
  end;

let qcLst : [File] =
  for fastq : File <- ( fastq1Lst+fastq2Lst ) do
    ( fastqc( fastq = fastq )|zip ) : File
  end;

let mpileupLst : [File] =
  for fa : File <- faLst do

    let <idx = idx : File> =
      bowtie2Build( fa = fa );

    let <fai = fai : File> =
      samtoolsFaidx( fa = fa );

    let sortedLst : [File] =
      for fastq1 : File <- fastq1Lst, fastq2 : File <- fastq2Lst do

        let <lst = splitLst1 : [File]> = split( file = fastq1 );
        let <lst = splitLst2 : [File]> = split( file = fastq2 );

        let bamLst : [File] =
          for split1 : File <- splitLst1,
              split2 : File <- splitLst2 do

            let <bam = bam : File> =
              bowtie2Align( idx    = idx,
                            fastq1 = split1,
                            fastq2 = split2 );

            ( samtoolsSort( bam = bam )|sorted ) : File
          end;

        ( samtoolsMerge( bamLst = bamLst )|merged ) : File

      end;

    let <merged = merged : File> =
      samtoolsMerge( bamLst = sortedLst );

    ( samtoolsMpileup( bam = merged,
                       fa  = fa,
                       fai = fai )|mpileup ) : File

  end;

let snpLst : [File] =
  for mpileup : File <- mpileupLst do
    ( varscan-snp( mpileup = mpileup )|vcf ) : File
  end;

let indelLst : [File] =
  for mpileup : File <- mpileupLst do
    ( varscan-indel( mpileup = mpileup )|vcf ) : File
  end;

let <fun = fun : File, exonicFun = exonicFun : File> =
  annovar( vcfLst   = ( snpLst+indelLst ),
           db       = db,
           buildVsn = buildVsn );


%% ============================================================
%% Query
%% ============================================================
<fun = fun, exonicFun = exonicFun, qcLst = qcLst>;

